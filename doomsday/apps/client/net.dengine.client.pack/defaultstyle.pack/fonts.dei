# Fonts for the default UI style
#
# - family: font family name
# - size can be "pt" or "px", defaults to "pt" 
# - weight: normal, bold, light
# - style: normal, italic
# - transform: normal, uppercase, lowercase

script { 
    import Version, gui, DisplayMode
     
    # Load the Open Sans font contained in this pack.
    def loadOpenSans()
        import App
        fontDir = __file__.fileNamePath() / "fonts"
        # Sadly, light fonts seem to not work in Qt presently.
        for style in ['Regular', 'Bold', 'Italic', 'BoldItalic']  #'Light', 'LightItalic'
            App.loadFont(fontDir / ("OpenSans-%s.ttf" % style))
        end    
    end
}

group {
    condition: Version.OS == 'windows'
    
    script { loadOpenSans() }

    font default {
        family: Open Sans
        size: 12pt
        weight: normal
        style: normal    
    }
    
    font monospace inherits default {
        family: Courier New
        size: 10pt
    }
}

group {
    condition: Version.OS == 'macx'

    script {
        # Define mappings for native font styles and weights.
        import App
        App.addFontMapping("SF UI Text", {
            ['regular', 25]: 'SFUIText-Light',
            ['regular', 50]: 'SFUIText-Regular',
            ['regular', 75]: 'SFUIText-Bold',
            ['italic',  25]: 'SFUIText-LightItalic',
            ['italic',  50]: 'SFUIText-Italic',
            ['italic',  75]: 'SFUIText-BoldItalic'
        })
        App.addFontMapping("SF UI Display", {
            ['regular', 25]: 'SFUIDisplay-Light',
            ['regular', 50]: 'SFUIDisplay-Regular',
            ['regular', 75]: 'SFUIDisplay-Bold',
            ['italic',  25]: 'SFUIText-LightItalic',    # Only non-italic available.
            ['italic',  50]: 'SFUIText-Italic',
            ['italic',  75]: 'SFUIText-BoldItalic'
        })
        App.addFontMapping("Menlo", {
            ['regular', 25]: 'Menlo-Regular',
            ['regular', 50]: 'Menlo-Regular',
            ['regular', 75]: 'Menlo-Bold',
            ['italic',  25]: 'Menlo-Italic',
            ['italic',  50]: 'Menlo-Italic',
            ['italic',  75]: 'Menlo-BoldItalic'
        })
    }
    
    font default {
        family: SF UI Text
        size $: gui.scale('16pt', DisplayMode.DPI_FACTOR)
        weight: normal
        style: normal
    }
    
    font monospace inherits default {
        family: Menlo
        size $: gui.scale('13pt', DisplayMode.DPI_FACTOR)
    }
}

group {
    condition: Version.OS == 'unix'

    font default {
        family: Liberation Sans
        size $: gui.scale("13pt", DisplayMode.DPI_FACTOR)
        weight: normal
        style: normal
    }

    font monospace inherits default {
        family: FreeMono
        size $: gui.scale("12pt", DisplayMode.DPI_FACTOR)
    }
}

font title inherits default {
    size $: gui.scale(self.size, 1.75)
    weight: light
}

font heading inherits title {
    size $: gui.scale(default.size, 1.2)
}

script {
    if Version.OS == 'macx'
        # Use the larger variant.
        title.family   = "SF UI Display"
        heading.family = "SF UI Display"
    end 
}

font small inherits default {
    size $: gui.scale(self.size, 0.75)
}

editor {
    font plaintext inherits default {}
    font hint inherits default {
        style: italic
        weight: light
    }
}

separator {
    font empty inherits default {
        size $: gui.scale(self.size, 0.5)
    }
    font label inherits default {
        condition: Version.OS == 'macx'
        size $: gui.scale(self.size, 0.7)
        transform: uppercase
    }
    font label inherits small {
        condition: Version.OS != 'macx'
        weight: bold
    }
    font annotation inherits small {}    
}

choice {
    font selected inherits default {
        weight: bold
    }
}

tab {
    font label inherits title {
        size $: gui.scale(self.size, 0.75)
    }
    font selected inherits tab.label {}
}

slider {
    font label inherits small {}
    font value inherits slider.label {
        weight: bold
    }
}

log {
    font normal inherits default {}
}
